<?xml version="1.0" encoding="ISO-8859-1"?>
<!-- $This file is distributed under the terms of the license in /doc/license.txt$ -->

<!-- See guidelines in vitro/doc/list_view_configuration_guidelines.txt -->

<list-view-config>
    <query-base>    
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX afn:  &lt;http://jena.hpl.hp.com/ARQ/function#&gt;
        
        SELECT DISTINCT ?role (afn:localname(?role) AS ?roleName)
                        ?roleLabel 
                        ?activity (afn:localname(?activity) AS ?activityName)
                        ?activityLabel
                        ?dateTimeStart ?dateTimeEnd WHERE {
            GRAPH ?g1 { ?subject ?property ?role } 
            OPTIONAL { GRAPH ?g2 { ?role core:roleIn ?activity }
                       OPTIONAL { GRAPH ?g3 { ?activity rdfs:label ?activityLabel } }
            }
            OPTIONAL { GRAPH ?g4 { ?role rdfs:label ?roleLabel } }
            OPTIONAL { GRAPH ?g5 { ?role core:dateTimeInterval ?dateTimeInterval }
                       OPTIONAL { GRAPH ?g6 { ?dateTimeInterval core:start ?dateTimeStartValue }
                                  GRAPH ?g7 { ?dateTimeStartValue core:dateTime ?dateTimeStart }
                       }
                       OPTIONAL { GRAPH ?g8 { ?dateTimeInterval core:end ?dateTimeEndValue }
                                  GRAPH ?g9 { ?dateTimeEndValue core:dateTime ?dateTimeEnd }
                       }
            }
        } ORDER BY DESC(?dateTimeEnd) DESC(?dateTimeStart) ?activityLabel ?activityName
    </query-base>
    
    <query-collated>    
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX afn:  &lt;http://jena.hpl.hp.com/ARQ/function#&gt;
        
        SELECT DISTINCT ?subclass
                        ?role (afn:localname(?role) AS ?roleName)
                        ?roleLabel 
                        ?activity ?activityName 
                        ?activityLabel
                        ?dateTimeStart ?dateTimeEnd WHERE {
            GRAPH ?g1 { ?subject ?property ?role } 
            OPTIONAL { GRAPH ?g2 { ?role core:roleIn ?activity }
                       OPTIONAL { GRAPH ?g3 { ?activity rdfs:label ?activityLabel } }
            }            
            OPTIONAL { GRAPH ?g4 { ?role a ?subclass }
                       GRAPH ?g5 { ?subclass rdfs:subClassOf core:Role }
            }   
            OPTIONAL { GRAPH ?g6 { ?role rdfs:label ?roleLabel } }
            OPTIONAL { GRAPH ?g7 { ?role core:dateTimeInterval ?dateTimeInterval }
                       OPTIONAL { GRAPH ?g8 { ?dateTimeInterval core:start ?dateTimeStartValue }
                                  GRAPH ?g9 { ?dateTimeStartValue core:dateTime ?dateTimeStart }
                       }
                       OPTIONAL { GRAPH ?g10 { ?dateTimeInterval core:end ?dateTimeEndValue }
                                  GRAPH ?g11 { ?dateTimeEndValue core:dateTime ?dateTimeEnd }
                       }
            }
        } ORDER BY ?subclass DESC(?dateTimeEnd) DESC(?dateTimeStart) ?activityLabel ?activityName
    </query-collated>
    
    <template>propStatement-hasRole.ftl</template>
</list-view-config>